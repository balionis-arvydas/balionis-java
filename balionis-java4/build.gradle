apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'maven-publish-auth'

group = 'com.balionis'
version = APP_VERSION
sourceCompatibility = 1.8

buildscript {
    dependencies {
        classpath 'org.hibernate.build.gradle:gradle-maven-publish-auth:2.0.1'
    }
    repositories {
        mavenCentral()
    }
}

repositories {
    maven {
        url "http://nexus.balionis.com:8081/repository/maven-releases/"
    }
    mavenCentral()
    mavenLocal()
}

dependencies {
    compile group: 'org.slf4j', name:'slf4j-api', version: '1.7.21'
    compile group: 'ch.qos.logback', name:'logback-core', version: '1.1.7'
    compile group: 'ch.qos.logback', name:'logback-classic', version: '1.1.7'

    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '2.8.+'
}

jar {
    manifest {
        attributes 'Implementation-Title': rootProject.name,
                   'Implementation-Version': version
    }
}

// gradle build
test {
    systemProperties 'property': 'value'
    testLogging {
        showStandardStreams true
        exceptionFormat 'full'
    }
}

// gradle publish
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            if(project.version.endsWith('-SNAPSHOT')) {
                name "InternalSnapshots"
                url "http://nexus.balionis.com:8081/repository/maven-snapshots/"
            } else {
                name "InternalReleases"
                url "http://nexus.balionis.com:8081/repository/maven-releases/"
            }
        }
    }
}

// ./gradlew run -Dmyvariable=myvalue -Dexec.args="myarg1 myarg2"
task run (type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath

    main = 'com.balionis.java4.MyApp'

    /* Or just each by name: */
    systemProperty "myvariable", System.getProperty("myvariable")

    /* Need to split the space-delimited value in the exec.args */
    if (System.getProperty("exec.args") != null) {
        args System.getProperty("exec.args").split()
    }
}